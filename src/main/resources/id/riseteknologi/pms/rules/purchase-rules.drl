package id.riseteknologi.pms.rules;

unit PurchaseUnit;

import id.riseteknologi.pms.rule.model.Product;
import id.riseteknologi.pms.rule.model.Supplier;
import id.riseteknologi.pms.rule.model.SupplierPriceChanged;
import id.riseteknologi.pms.rule.model.PurchaseDecision;
import java.math.BigDecimal;

import function id.riseteknologi.pms.rules.PurchaseUnit.isLessThanPriceThreshold;
import function id.riseteknologi.pms.rules.PurchaseUnit.isLessThan;

rule "product watched"
salience 200
when
	$product: /product
then
	System.out.println("PRODUCT FOUND");
	purchaseDecision.setProductId($product.getId());
end

rule "rise stock empty"
salience 100
when
	$rise: /rise[ stock == 0 ]
	$supplier: /suppliers[ stock > 0, currentPrice < previousPrice, hasBeenProcessed == false ]
then
	modify($supplier) {
		setHasBeenProcessed(true);
	}
	eligibleSuppliers.add($supplier);
	System.out.println("Rise stock empty, supplier found before " + maxEligiblePrice);
	maxEligiblePrice.setValue(maxEligiblePrice.getValue().max($supplier.getCurrentPrice()));
	System.out.println("Rise stock empty, supplier found " + maxEligiblePrice);
end

rule "rise stock less than 100"
salience 100
when
	$rise: /rise[ stock > 0, stock < 100 ]
	$supplier: /suppliers[ stock > 0, currentPrice < previousPrice, currentPrice < $rise.price, hasBeenProcessed == false ]
then
	modify($supplier) {
		setHasBeenProcessed(true);
	}
	eligibleSuppliers.add($supplier);
	System.out.println("Rise stock less than 100, supplier found before " + maxEligiblePrice);
	maxEligiblePrice.setValue(maxEligiblePrice.getValue().max($supplier.getCurrentPrice()));
	System.out.println("Rise stock less than 100, supplier found " + maxEligiblePrice);
end

rule "rise stock 100 - 199"
salience 100
when
	$rise: /rise[ stock >= 100, stock < 200 ]
	$supplier: /suppliers[ stock > 0, currentPrice < previousPrice]
	eval(isLessThanPriceThreshold($supplier.currentPrice, $rise.price, priceThreshold))
then
	modify($supplier) {
		setHasBeenProcessed(true);
	}
	eligibleSuppliers.add($supplier);
	System.out.println("Rise stock 100-199, supplier found before " + maxEligiblePrice);
	maxEligiblePrice.setValue(maxEligiblePrice.getValue().max($supplier.getCurrentPrice()));
	System.out.println("Rise stock 100-199, supplier found " + maxEligiblePrice);
end

rule "price not less than previous but cheaper than maximum eligible supplier"
salience 50
when
	$supplier: /suppliers[ hasBeenProcessed == false ]
	eval(isLessThan($supplier.currentPrice, maxEligiblePrice))
then
	System.out.println("Supplier current cheaper found, currentPrice: " + $supplier.getCurrentPrice());
	System.out.println("Supplier current cheaper found, maxEligible Price: " + maxEligiblePrice);
	modify($supplier) {
		setHasBeenProcessed(true);
	}
	eligibleSuppliers.add($supplier);
end

rule "supplier product not eligible"
when
	$supplier: /suppliers[ hasBeenProcessed == false ]
then
	modify($supplier) {
		setHasBeenProcessed(true);
	}
	System.out.println("Supplier not eligible found, maxEligible Price: " + maxEligiblePrice);
end



rule "all suppliers eligibility have been determined"
salience -1
when
	not /suppliers[ hasBeenProcessed == false ]
then
	System.out.println("All suppliers have been processed");
	purchaseDecision.processEligibleSuppliers(eligibleSuppliers, maxBuy);
end